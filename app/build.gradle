plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-parcelize'
    id 'kotlin-kapt'
    id 'com.google.gms.google-services'
    id 'dagger.hilt.android.plugin'
}

android {
    namespace 'com.example.projectnailsschedule'
    compileSdk = 34

    defaultConfig {
        applicationId "com.example.projectnailsschedule"
        minSdk = 23
        targetSdk = 34
        versionCode = 26
        versionName = "7.1"

        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"

        kapt {
            arguments {
                arg("room.schemaLocation", "$projectDir/schemas")
            }
        }
    }

    signingConfigs {
        debug {
            storeFile file(project.properties['DEBUG_STORE_FILE_PATH'])
            storePassword project.properties['DEBUG_STORE_PASSWORD']
            keyPassword project.properties['DEBUG_KEY_PASSWORD']
            keyAlias project.properties['DEBUG_KEY_ALIAS']
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
        coreLibraryDesugaringEnabled = true
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    kotlin {
        jvmToolchain(17)
    }

    buildFeatures {
        viewBinding = true
    }

    flavorDimensions += "MySchedule"

    productFlavors {
        Rustore {
            dimension "MySchedule"
            applicationId "com.example.projectnailsschedule"
            versionCode = 25
            versionName = "7.0"
        }

        GooglePlay {
            dimension "MySchedule"
            applicationId "com.lemmiwinks.myschedule"
            versionCode = 62
            versionName = "7." + versionCode
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.11.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.7.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.7.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.7.0'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.7.7'
    implementation 'androidx.navigation:navigation-ui-ktx:2.7.7'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'androidx.activity:activity:1.8.2'
    implementation 'androidx.fragment:fragment:1.6.2'
    implementation 'androidx.fragment:fragment-ktx:1.6.2'
    implementation 'androidx.preference:preference-ktx:1.2.1'
    implementation 'androidx.viewpager2:viewpager2:1.0.0'

    // WorkManager
    implementation "androidx.work:work-runtime-ktx:2.8.1"
    implementation "androidx.hilt:hilt-work:1.0.0"
    kapt "androidx.hilt:hilt-compiler:1.0.0"

    // Google/Firebase
    implementation 'com.google.firebase:firebase-database-ktx:20.3.1'
    implementation 'com.google.android.play:app-update:2.1.0'
    implementation 'com.google.android.play:app-update-ktx:2.1.0'

    // Room
    implementation "androidx.room:room-runtime:2.6.1"
    implementation "androidx.room:room-ktx:2.6.1"
    kapt 'androidx.room:room-compiler:2.6.1'
    androidTestImplementation "androidx.room:room-testing:2.6.1"
    androidTestImplementation "com.google.truth:truth:1.1.3"

    // Hilt
    implementation "com.google.dagger:hilt-android:2.48.1"
    kapt "com.google.dagger:hilt-compiler:2.48.1"

    // Ad SDK
    implementation 'com.my.target:mytarget-sdk:5.16.5'

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation "com.squareup.retrofit2:converter-gson:2.9.0"
    implementation "com.squareup.okhttp3:okhttp:4.7.2"
    implementation "com.squareup.okhttp3:logging-interceptor:4.7.2"

    // Misc
    implementation 'com.github.dhaval2404:imagepicker:2.1'
    implementation 'com.auth0.android:jwtdecode:2.0.0'
    implementation 'io.coil-kt:coil:2.2.2'
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.0.4'

    // Rustore SDK
    implementation platform("ru.rustore.sdk:bom:2025.05.02")
    implementation "ru.rustore.sdk:appupdate"
    implementation "ru.rustore.sdk:review"
    implementation "ru.rustore.sdk:billingclient"

    // RxJava
    implementation 'io.reactivex.rxjava3:rxjava:3.1.9'

    // Unit Tests
    testImplementation 'junit:junit:4.13.2'
    testImplementation "org.junit.jupiter:junit-jupiter:5.8.2"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.8.2"
    testRuntimeOnly "org.junit.vintage:junit-vintage-engine:5.8.2"
    testImplementation "org.mockito:mockito-core:4.0.0"
    testImplementation "org.mockito.kotlin:mockito-kotlin:4.0.0"

    // Android Tests
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
    testLogging {
        exceptionFormat = "full"
        events "standardOut", "started", "passed", "skipped", "failed"
        showStandardStreams = true
    }
}
